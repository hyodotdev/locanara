group 'dev.hyodot.flutter_ondevice_ai'
version '0.1.0'

buildscript {
    ext.kotlin_version = '2.0.21'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

// Read Locanara version from locanara-versions.json (Single Source of Truth)
def getLocanaraVersion() {
    def candidates = [
        new File(project.projectDir, "../../../locanara-versions.json"),
        new File(rootProject.projectDir, "../../../locanara-versions.json"),
        new File(rootProject.projectDir, "../../../../locanara-versions.json"),
    ]
    for (candidate in candidates) {
        if (candidate.exists()) {
            def versionJson = candidate.text
            def matcher = (versionJson =~ /"android"\s*:\s*"([^"]+)"/)
            if (matcher) return matcher[0][1]
        }
    }
    return "1.1.0"
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace "dev.hyodot.flutter_ondevice_ai"
    compileSdk 35

    defaultConfig {
        minSdk 31
        targetSdk 35
        versionCode 1
        versionName "0.1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
        freeCompilerArgs += ["-Xskip-metadata-version-check"]
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"
    implementation "com.locanara:locanara:${getLocanaraVersion()}"
}
