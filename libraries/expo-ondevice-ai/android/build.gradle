apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

group = 'expo.modules.ondeviceai'
version = '0.1.0'

// Read Locanara version from locanara-versions.json (Single Source of Truth)
def getLocanaraVersion() {
  def versionsFile = new File(rootProject.projectDir, "../../../locanara-versions.json")
  def versionJson = versionsFile.text
  // Extract android version using regex: "android": "1.0.2"
  def matcher = (versionJson =~ /"android"\s*:\s*"([^"]+)"/)
  return matcher ? matcher[0][1] : "1.0.2"
}

def expoModulesCorePlugin = new File(project(":expo-modules-core").projectDir.absolutePath, "ExpoModulesCorePlugin.gradle")
apply from: expoModulesCorePlugin
applyKotlinExpoModulesCorePlugin()
useCoreDependencies()
useExpoPublishing()

// If you want to use the managed Android SDK versions from expo-modules-core, set this to true.
def useManagedAndroidSdkVersions = false
if (useManagedAndroidSdkVersions) {
  useDefaultAndroidSdkVersions()
} else {
  buildscript {
    ext.safeExtGet = { prop, fallback ->
      rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    }
  }
  project.android {
    compileSdkVersion safeExtGet("compileSdkVersion", 34)
    defaultConfig {
      minSdkVersion safeExtGet("minSdkVersion", 24)
      targetSdkVersion safeExtGet("targetSdkVersion", 34)
    }
  }
}

android {
  namespace "expo.modules.ondeviceai"
  defaultConfig {
    versionCode 1
    versionName "0.1.0"
  }
  lintOptions {
    abortOnError false
  }
  kotlinOptions {
    jvmTarget = "17"
    freeCompilerArgs += ["-Xskip-metadata-version-check"]
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7"
  // Version is read from locanara-versions.json via getLocanaraVersion()
  implementation "com.locanara:locanara:${getLocanaraVersion()}"
}
